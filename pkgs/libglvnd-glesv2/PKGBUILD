# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: TÃ©rence Clastres <t.clastres@gmail.com>

pkgname=libglvnd-glesv2
_pkgname=${pkgname/-glesv2}
pkgver=1.2.0
pkgrel=3
pkgdesc="The GL Vendor-Neutral Dispatch library patched to add glesv2 and other pkgconfig data removed from mesa 19.1"
arch=('x86_64')
url="https://github.com/NVIDIA/libglvnd"
license=('custom:BSD-like')
depends=('mesa>=19.1')
makedepends=('libxext' 'libx11' 'glproto' 'python')
provides=('libglvnd' 'libgl' 'libegl' 'libgles')
conflicts=('libglvnd')
source=("$pkgname-$pkgver.tar.gz::https://github.com/NVIDIA/libglvnd/archive/v$pkgver.tar.gz"
        LICENSE
	0001-Add-a-configure-option-to-disable-glesv1-or-glesv2.patch::https://git.archlinux.org/svntogit/packages.git/plain/trunk/0001-Add-a-configure-option-to-disable-glesv1-or-glesv2.patch?h=packages/libglvnd)
sha512sums=('0e0e9c3f303739c73f5bee4c2aaef9eff56440345f2f1ce285ee98eb94c1e5e740774dd501aeeed1299aa25a449d813a850e95282999a3ca4648d2381d6c8fa6'
            'bf0f4a7e04220a407400f89226ecc1f798cc43035f2538cc8860e5088e1f84140baf0d4b0b28f66e4b802d4d6925769a1297c24e1ba39c1c093902b2931781a5'
            'b2d2e8fd7ea92bfc913d0b808d15c801e7c3ceb484bdd60700fd1c9ce27b4fbd4b68e568e967599f2e5132cc1ed382ec87a07c16bd3bfa5c4d920f2d9600e689')


prepare() {
  cd $_pkgname-$pkgver

  # merged upstream
  patch -Np1 -i ../0001-Add-a-configure-option-to-disable-glesv1-or-glesv2.patch

  ./autogen.sh
}

build() {
  cd $_pkgname-$pkgver
  ./configure --prefix=/usr \
    --disable-gles1
  make
}

package() {
  # libglvnd needs mesa for indirect rendering
  depends=('libxext' 'mesa' 'opengl-driver')

  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
