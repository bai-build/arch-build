# Maintainer: Evert Van Petegem <evert [at] vanpetegem [dot] me>
# Contributor: Daniel Landau <daniel@landau.fi>
# Contributor: Maxqia <contrib@maxqia.com>

# The following people have contributed to keepassx-git package
# Contributor: Lev Lybin <aur@devtrue.net>
# Contributor: Jamie Macdonald <jamie.alban@gmail.com>
# Contributor: Alucryd <alucryd at gmail dot com>
# Contributor: Paolo Stivanin <admin at polslinux dot it>

pkgname=keepassxc-browser-git
_gitname=keepassxc
pkgver=2.1.4.r620.gc7dd20a0
pkgrel=1
pkgdesc="A reboot of an OpenSource password safe which helps you to manage your passwords in an easy and secure way with a modern browser extension"
arch=('i686' 'x86_64')
url="https://github.com/varjolintu/keepassxc"
license=('GPL2')
depends=('libxtst' 'shared-mime-info' 'qt5-x11extras' 'hicolor-icon-theme' 'desktop-file-utils' 'yubikey-personalization' 'libsodium')
install=keepassxc-browser.install
makedepends=('git' 'intltool' 'cmake' 'qt5-base' 'qt5-tools' 'zlib' 'libgcrypt')
conflicts=('keepassxc' 'keepassxc-git')
replaces=('keepassx-http' 'keepassx-reboot-git')
options=(!emptydirs)
source=(git+https://github.com/varjolintu/keepassxc.git#branch=feature/chromeKeePassXC)
md5sums=('SKIP')

pkgver() {
    cd "${_gitname}"
    git describe --long | sed 's/^FOO-//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "${_gitname}"
    mkdir -p build
}

build() {
    cd "${_gitname}/build"
    cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_BINDIR=/usr/bin \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DCMAKE_VERBOSE_MAKEFILE=OFF \
        -DWITH_TESTS=OFF \
        -DWITH_XC_AUTOTYPE=ON \
        -DWITH_XC_YUBIKEY=ON \
        -DWITH_XC_BROWSER=ON \
        -DCMAKE_BUILD_TYPE=Release ..
    make
}

#check() {
#    cd "${_gitname}/build"
#    make test
#}

package() {
    cd "${_gitname}/build"
    make DESTDIR="${pkgdir}" install
}

