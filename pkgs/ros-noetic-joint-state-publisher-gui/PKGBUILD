pkgdesc="ROS - This package contains a GUI tool for setting and publishing joint state values for a given URDF."
url='http://wiki.ros.org/joint_state_publisher'

pkgname='ros-noetic-joint-state-publisher-gui'
pkgver='1.15.0'
arch=('any')
pkgrel=1
license=('BSD')

ros_makedepends=(
    ros-noetic-catkin
)
makedepends=(
    cmake
    ros-build-tools
    ${ros_makedepends[@]}
)

ros_depends=(
    ros-noetic-joint-state-publisher
    ros-noetic-python-qt-binding
    ros-noetic-rospy
)

depends=(
    ${ros_depends[@]}
)

_dir="joint_state_publisher-${pkgver}/joint_state_publisher_gui"
source=("${pkgname}-${pkgver}.tar.gz"::"https://github.com/ros/joint_state_publisher/archive/${pkgver}.tar.gz")
sha256sums=('3306a14bc2eac06d6faecc458766c9fc654c9fde062a5e676479772aab3c39cd')

build() {
    # Use ROS environment variables
    source /usr/share/ros-build-tools/clear-ros-env.sh
    [ -f /opt/ros/noetic/setup.bash ] && source /opt/ros/noetic/setup.bash

    # Create the build directory
    [ -d ${srcdir}/build ] || mkdir ${srcdir}/build
    cd ${srcdir}/build

    # Build the project
    cmake ${srcdir}/${_dir} \
            -DCATKIN_BUILD_BINARY_PACKAGE=ON \
            -DCMAKE_INSTALL_PREFIX=/opt/ros/noetic \
            -DPYTHON_EXECUTABLE=/usr/bin/python \
            -DSETUPTOOLS_DEB_LAYOUT=OFF
    make
}

package() {
    cd "${srcdir}/build"
    make DESTDIR="${pkgdir}/" install
}
