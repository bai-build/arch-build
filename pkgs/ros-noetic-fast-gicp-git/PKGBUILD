# Maintainer: Achmad Fathoni<fathoni.id(at)gmail.com>

pkgdesc="OpenMP boosted NDT and GICP algorithms with CUDA enabled"
url='https://github.com/SMRT-AIST/fast_gicp'
pkgname='ros-noetic-fast-gicp-git'
pkgver=r142.68b4364
arch=(any)
pkgrel=3
license=(BSD)
provides=(${pkgname::-4})
cuda=ON #compile with CUDA support
makedepends=(cmake ros-build-tools gcc10)
optdepends=(openmp)
depends=(pcl cuda eigen)

source=(
  $pkgname::git+https://github.com/SMRT-AIST/fast_gicp
  nvbio::https://github.com/NVlabs/nvbio/archive/9bb7e6363c65f65e46f21df09bef98e404250f10.tar.gz
)

sha256sums=(
  'SKIP'
  'd8208f7044ab26f8b9dc87898a0265c57346d171375e9e4d8669807cada955cb'
)
 
pkgver() {
  cd "$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare(){
  cd $srcdir
  mv -n nvbio-9bb7e6363c65f65e46f21df09bef98e404250f10/* $pkgname/thirdparty/nvbio
}

build() {
  # Use ROS environment variables
  source /usr/share/ros-build-tools/clear-ros-env.sh
  [ -f /opt/ros/noetic/setup.bash ] && source /opt/ros/noetic/setup.bash

  # Build project
  cmake -Wno-dev -B build -S ${pkgname} \
          -DCATKIN_ENABLE_TESTING=0 \
          -DCATKIN_BUILD_BINARY_PACKAGE=ON \
          -DCMAKE_INSTALL_PREFIX=/opt/ros/noetic \
          -DPYTHON_EXECUTABLE=/usr/bin/python3 \
          -DSETUPTOOLS_DEB_LAYOUT=OFF \
          -DBUILD_VGICP_CUDA=$cuda \
          -DCMAKE_CXX_STANDARD=17 \
          -DCUDA_HOST_COMPILER=/usr/bin/gcc-10
}

package() {
  cd build
  make DESTDIR="${pkgdir}/" install
}
