# Contributor: Eric Schulte <eschulte@grammatech.com>
# Maintainer: Eric Schulte <eschulte@grammatech.com>
_srcname=ddisasm
pkgname=ddisasm-git
pkgver=v0.1.1.r628.gcd19fb5
pkgrel=1
pkgdesc="A fast and accurate disassembler"
arch=('x86_64')
url="https://github.com/grammatech/ddisasm"
license=('aGPLv3')
depends=('capstone' 'libehp' 'boost')
makedepends=('git' 'cmake' 'souffle' 'mcpp' 'lief' 'gtirb' 'gtirb-pprinter')
provides=('ddisasm')
source=('git+https://github.com/grammatech/ddisasm.git')
sha512sums=('SKIP')

pkgver() {
  cd "$_srcname"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd "$_srcname/"
    cmake . -Bbuild -DCMAKE_INSTALL_PREFIX=/usr -DLIEF_ROOT=/usr -DLIEF_LIBRARY=/usr/lib/libLIEF.so
    make -C build
}

package() {
  cd "$_srcname/build"
  make DESTDIR="$pkgdir" install
}
