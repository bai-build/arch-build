# Maintainer: Davide Depau <davide@depau.eu>

pkgname=networkmanager-wireguard-git
pkgver=r87.0e1124d
pkgrel=2
pkgdesc='NetworkManager VPN plugin for WireGuard - git'
arch=('i686' 'x86_64' 'armv7h' 'aarch64' 'pentium4')
license=('GPL')
url='https://github.com/max-moser/network-manager-wireguard/'
depends=('networkmanager' 'libnma' 'WIREGUARD-MODULE' 'wireguard-tools')
makedepends=('git' 'intltool')
provides=('networkmanager-wireguard')
conflicts=('networkmanager-wireguard')
source=("$pkgname::git+https://github.com/max-moser/network-manager-wireguard.git")
sha256sums=('SKIP')

pkgver() {
	cd "$pkgname"
	( set -o pipefail
		git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
		printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	)
}

prepare() {
	cd $pkgname

	./autogen.sh --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--libexecdir=/usr/lib/networkmanager \
		--enable-more-warnings=no \
		--with-libnm-glib=no \
		--disable-static
}

build() {
	make -C "$pkgname"
}

package() {
	make -C "$pkgname" DESTDIR="${pkgdir}" sysconfdir=/etc libdir=/usr/lib install
}

