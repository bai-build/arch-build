# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
_pkgname=kgraphviewer
pkgname=kgraphviewer-frameworks-git
pkgver=2.20.f1c3420
pkgrel=1
pkgdesc='A Graphviz dot graph file viewer for KDE'
arch=(i686 x86_64)
url='http://www.kde.org/applications/graphics/kgraphviewer/'
license=(GPL FDL)
conflicts=($_pkgname)
provides=($_pkgname)
depends=(qt5-base qt5-svg kcoreaddons kdoctools kparts kio kwidgetsaddons kiconthemes graphviz)
makedepends=(cmake boost docbook-xsl)
source=('git://anongit.kde.org/kgraphviewer#branch=frameworks')
md5sums=(SKIP)

pkgver() {
  cd $_pkgname
  {
    git describe --long 2>/dev/null || echo "2.20.$(git rev-parse --short HEAD)"
  } | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build(){
  cd "$srcdir"

  mkdir -p build
  cd build
  cmake ../$_pkgname \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib
  make
}

package() {
  cd "$srcdir/build"

  make DESTDIR="$pkgdir" install
}
