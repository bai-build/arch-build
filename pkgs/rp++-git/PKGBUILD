# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Jan Lieven <echo amFuK2F1ckBkYXMtbGFib3Iub3JnCg== | base64 -d>

pkgname=rp++-git
pkgver=1+13+g3a54a7c
pkgrel=2
epoch=1
pkgdesc='Find ROP gadgets in PE/ELF/MACH-O x86/x64 binaries'
url='https://github.com/0vercl0k/rp'
arch=('i686' 'x86_64')
license=('GPL3')
depends=('gcc-libs')
makedepends=('git' 'cmake')
provides=('rp++')
conflicts=('rp++')
source=(${pkgname}::git+https://github.com/0vercl0k/rp)
sha512sums=('SKIP')

pkgver() {
  cd ${pkgname}
  git describe --tags|sed 's|-|+|g'|sed -r 's|v?(.+)|\1|g'
}

prepare() {
  cd ${pkgname}
  mkdir build
  # TODO fix and remove static build
  #sed 's/-static//g' -i CMakeLists.txt
}

build() {
  cd ${pkgname}/build
  export CFLAGS+=" -fPIC"
  export CXXFLAGS+=" -fPIC"
  export CPPFLAGS+=" -fPIC"
  VERBOSE=1 cmake ..
  VERBOSE=1 make
}

check() {
  cd ${pkgname}/bin
  ./rp* -f bin_tests/elf-x86-bash-v4.1.5.1 --search-hexa="main"
}

package() {
  cd ${pkgname}
  install -Dm 755 bin/rp-lin-* "${pkgdir}/usr/bin/rp"
}

# vim: ts=2 sw=2 et:
