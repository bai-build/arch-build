# Maintainer: EatMyVenom <eat.my.venomm@gmail.com>
# Contributor: Christian Hesse <mail@eworm.de>
# Contributor: Pierre Schmitz <pierre@archlinux.de> ([core] package)
# Contributor: Fran√ßois Charette <firmicus@gmx.net>

pkgname=xz-git
pkgver=5.3.1alpha.r134.ga35a69d
pkgrel=1
pkgdesc='Library and command line tools for XZ and LZMA compressed files - git checkout'
arch=('x86_64')
url='http://tukaani.org/xz/'
license=('GPL' 'LGPL' 'custom')
depends=('sh')
makedepends=('git' 'po4a')
provides=('lzma' 'lzma-utils' 'xz-utils' "xz=${pkgver%%.r*}")
replaces=('lzma' 'lzma-utils' 'xz-utils')
conflicts=('lzma' 'lzma-utils' 'xz-utils' 'xz')
options=('!libtool')
source=('git+http://git.tukaani.org/xz.git')
sha256sums=('SKIP')

# keep an upgrade path for older installations
PKGEXT='.pkg.tar.gz'

pkgver() {
	cd xz/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[-_+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}

build() {
	cd xz/

	./autogen.sh
	./configure --prefix=/usr \
		--disable-rpath 
	make
}

check() {
	cd xz/

	make check
}

package() {
	cd xz/
	make DESTDIR=${pkgdir} install
	install -d -m0755 ${pkgdir}/usr/share/licenses/xz/
	ln -s /usr/share/doc/xz/COPYING ${pkgdir}/usr/share/licenses/xz/
}

