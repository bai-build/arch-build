# Maintainer: Felix Rohrbach <kde@fxrh.de>
# Contributor: Spike29 <leguen.yannick@gmail.com>
# Contributor: Samir Faci <csgeek@archlinux.us>
# Contributor: TimothÃ©e Ravier <tim@siosm.fr>

_realname='qxmpp'
pkgname='qxmpp-qt5'
pkgver='0.9.3'
pkgrel='1'
pkgdesc='An XMPP client library based on Qt & C++'
arch=('i686' 'x86_64')
url='https://github.com/qxmpp-project/qxmpp'
license=('LGPL2.1')
depends=('qt5-base')
optdepends=('speex: required to enable speex audio codec'
            'libvpx: required to enable vpx video codec'
	    'libtheora: required to enable theora video codec') 
conflicts=('qxmpp-git' 'qxmpp-leechcraft-git' 'qxmpp-qt5-git' 'qxmpp')
source=("${url}/archive/v${pkgver}.tar.gz")
sha1sums=('f9391bedd5153ae19f68f25276bf2624c5020d09')
 
build() {
	cd "$srcdir/$_realname-$pkgver/"
	[ -d build ] || mkdir build && cd build
	qmake-qt5 PREFIX=/usr ..

	# In order to enable speex audio codec, vpx video codec or theora video codec,
	# add QXMPP_USE_SPEEX=1, QXMPP_USE_VPX=1 and QXMPP_USE_THEORA=1 to qmake arguments.

	make
}

package() {
	cd "$srcdir/$_realname-$pkgver/build"
	make INSTALL_ROOT="$pkgdir" install
}
