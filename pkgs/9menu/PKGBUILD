# Maintainer: Brian Bidulock <bidulock@openss7.org>
# Contributor: Andreas Wagner <AndreasBWagner@pointfree.net>
# Contributor: Niklas Schmuecker (IRC: nisc) <nschmuecker (gmail)>

pkgname=9menu
pkgver=1.10
pkgrel=1
pkgdesc='Simple and easily configurable X-menu / program launcher.'
arch=('i686' 'x86_64')
license=('custom')
url='ftp://freefriends.org/arnold/Source/'
depends=('libxext')
makedepends=('imake')
source=("ftp://freefriends.org/arnold/Source/$pkgname-$pkgver.shar.gz")
md5sums=('ea9590432b933a5666d0e007af5cf55d')

prepare() {
  cd "$srcdir"
  test -d $pkgname-$pkgver || mkdir $pkgname-$pkgver
  cd $pkgname-$pkgver
  sh ../$pkgname-$pkgver.shar
  tail -n22 README > LICENSE
  ln -s 9menu.1 9menu.man
}
build() {
  cd "$srcdir/$pkgname-$pkgver"
  xmkmf
  make
}
package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install install.man
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/9menu/license.txt"
}
