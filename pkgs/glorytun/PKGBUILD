# Maintainer: Adrien GallouÃ«t <adrien@gallouet.fr>
pkgname=glorytun
pkgver=0.3.4
pkgrel=3
pkgdesc="Multipath UDP tunnel"
arch=('x86_64')
url="https://github.com/angt/glorytun"
license=('BSD')
depends=('libsodium' 'iproute2')
makedepends=('git' 'libsodium' 'pkg-config')
source=("git+$url#tag=v$pkgver")
md5sums=('SKIP')

prepare() {
  cd "$srcdir/$pkgname"
  git submodule update --init --recursive
}

build() {
  cd "$srcdir/$pkgname"
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir/" install-strip
  install -D -m0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
