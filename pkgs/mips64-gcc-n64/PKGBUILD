# Maintainer: Joey Dumont <joey.dumont@gmail.com>
pkgname=mips64-gcc-n64
pkgver=7.2.0
pkgrel=1
arch=('x86_64' 'mips64')
pkgdesc="GCC for the N64 toolchain"
url="https://ftp.gnu.org/gnu/gcc/"
source=("gcc.tar.gz::https://ftp.gnu.org/gnu/gcc/gcc-${pkgver}/gcc-${pkgver}.tar.gz")
md5sums=('2e4be17c604ea555e0dff4a8f81ffe44')
options=('!buildflags' '!strip' 'staticlibs')
depends=('n64-toolchain' 'mips64-binutils-n64' 'mips64-gcc-stage1-n64' 'mips64-newlib-n64')
license=('GPL')

# Local variables
_prefix=/opt/n64-dev/usr
_target=mips64

prepare() {

  # -- We configure gcc.
  mkdir {gcc,build-gcc}
  tar xf gcc.tar.gz --strip-components=1 -C gcc

  # -- Download prerequesites.
  cd gcc; ./contrib/download_prerequisites; cd ../

  # -- Configure gcc.
  cd build-gcc
  ../gcc/configure  --prefix=${_prefix}                      \
                    --target=${_target}                      \
                    --with-arch=vr4300                       \
                    --with-abi=32                            \
                    --with-newlib                            \
                    --with-sysroot=/opt/n64-dev/n64-sysroot/ \
                    --enable-langues=c,c++                   \
                    --enable-target-optspace                 \
                    --enable-sjlj-exceptions                 \
                    --disable-multilib                       \
                    --disable-threads                        \
                    --disable-nls
}

build() {

  # -- We build gcc executables.
  cd $srcdir/build-gcc/
  make -j4 all
}

package() {

  cd $srcdir/build-gcc/
  make DESTDIR="$pkgdir" install

  # -- Remove files that were generated by stage1.
  rm -r "${pkgdir}/${_prefix}/bin"
  rm -r "${pkgdir}/${_prefix}/include"
  rm -r "${pkgdir}/${_prefix}/lib/gcc/${_target}/${pkgver}/finclude"
  rm -r "${pkgdir}/${_prefix}/lib/gcc/${_target}/${pkgver}/include-fixed"
  find "${pkgdir}/${_prefix}/lib/gcc/${_target}/${pkgver}/include" -maxdepth 1 -type f  ! -name gcov.h ! -name unwind.h -exec rm {} +
  rm -r "${pkgdir}/${_prefix}/lib/gcc/${_target}/${pkgver}/install-tools"
  rm "${pkgdir}/${_prefix}/lib/gcc/${_target}/${pkgver}/plugin/gtype.state"
  rm -r "${pkgdir}/${_prefix}/lib/gcc/${_target}/${pkgver}/plugin/include"
  rm -r "${pkgdir}/${_prefix}/libexec/"
  find "${pkgdir}/${_prefix}/share/info/" -maxdepth 1 -type f  ! -name libquadmath.h -exec rm {} +
  rm -r "${pkgdir}/${_prefix}/share/man"

}
