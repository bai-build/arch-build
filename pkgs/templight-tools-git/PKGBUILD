# Maintainer: nontlikeuname

pkgname=templight-tools-git
pkgver=2b2322b
pkgrel=1
pkgdesc="Tools to deal with template instantiation profiles generated by templight"
arch=(any)
license=('GPL')
depends=()
makedepends=('git' 'cmake' 'clang-trunk' 'ninja')
#checkdepends=(gcc-objc gnustep-make vala rust gcc-fortran mono boost qt5-base) #gtest or googletest
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/mikael-s-persson/templight-tools')
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "%s" "$(git describe --long --always | sed 's/\([^-]*-\)g/r\1/;s/-/./g')"
}

prepare() {
	mkdir -p "${srcdir}/build"
}

build() {
	cd "$srcdir/build"
	cmake -GNinja -DCMAKE_INSTALL_PREFIX:PATH=/usr "../${pkgname%-git}"
	ninja
}

package() {
	cd "$srcdir/build"
	DESTDIR=${pkgdir} ninja install
}
