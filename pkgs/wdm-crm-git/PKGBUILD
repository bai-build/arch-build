# Maintainer: Brian Bidulock <bidulock at openss7 dot org>

pkgname=wdm-crm-git
_pkgname=wdm
pkgver=1.28.r40.gd9a51b4
pkgrel=1
pkgdesc="A replacement X window display manager similar to, and based on XDM"
arch=(i686 x86_64)
url="https://github.com/bbidulock/wdm"
license=('GPL')
depends=('windowmaker-crm-git' 'libdbus')
makedepends=('git')
provides=("$_pkgname")
conflicts=("$_pkgname" "$_pkgname-git")
source=("$pkgname::git+https://github.com/bbidulock/wdm.git#branch=next"
	build.patch)
md5sums=('SKIP'
         '38cc79eaa37e8fc0fb6967c25b3afc07')

pkgver() {
  cd $pkgname
  git describe --long | sed -r 's/wdm-//;s/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  cd $pkgname
  patch -Np2 -b -z .orig <../build.patch
  autoreconf -fiv
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --localedir=/usr/share/locale \
	--sysconfdir=/etc/wdm --with-wdmdir=/etc/wdm --mandir=/usr/share/man \
	--with-fakehome=/var/empty \
	--enable-aafont --enable-exitlogin
  rm -f po/stamp-po
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -Dm755 bin/TestLogin "$pkgdir/usr/bin/wdmTestLogin"
  install -Dm755 bin/wdmChooser "$pkgdir/usr/bin/wdmChooser"
  install -Dm755 bin/wdmPrefs "$pkgdir/usr/bin/wdmPrefs"
  # used /etc/pam.d/xscreensaver as an example
  #
  install -d "$pkgdir/etc/pam.d"
  echo "auth	required	pam_unix_auth.so" > "$pkgdir/etc/pam.d/wdm"
  chmod 0644 "$pkgdir/etc/wdm/wdm-config"
  install -d "$pkgdir/etc/wdm/authdir"
  install -d "$pkgdir/var/empty"
}
