# Maintainer: Antonio Rojas 

pkgname=ksnapshot-frameworks-git
pkgver=r684.5900675
pkgrel=1
pkgdesc='Screen Capture Program'
arch=('i686' 'x86_64')
url='http://kde.org/applications/graphics/ksnapshot/'
license=('GPL')
depends=('libkipi-frameworks-git' 'kparts')
makedepends=('extra-cmake-modules' 'git' 'kdoctools' 'python')
conflicts=('kdegraphics-ksnapshot' 'ksnapshot')
provides=('ksnapshot')
source=("git://anongit.kde.org/ksnapshot.git#branch=frameworks")
install=$pkgname.install
sha256sums=('SKIP')

pkgver() {
  cd ksnapshot
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() { 
  cd build
  cmake ../ksnapshot \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
