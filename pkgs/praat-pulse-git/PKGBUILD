# Maintainer: Yadav Gowda <yadav . gowda __at__ gmail . com>
pkgname=praat-pulse-git
pkgver=r1175.e1cb2d3
pkgver() {
    cd "${pkgname%-git}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}
pkgrel=1
pkgdesc="A tool for 'Doing Phonetics by computer' (compiled with PulseAudio support)"
arch=('x86_64' 'i686')
url="http://www.fon.hum.uva.nl/praat/"
license=('GPL')
depends=('gtk2' 'alsa-lib')
makedepends=('git' 'pkg-config' 'gtk2' 'alsa-lib')
optdepends=('ttf-sil-fonts')
provides=('praat')
conflicts=('praat' 'praat-git')
source=('praat-pulse::git+https://github.com/praat/praat.git')
md5sums=('SKIP')

prepare() {
    cd "$srcdir/${pkgname%-git}"
    cp makefiles/makefile.defs.linux.pulse makefile.defs
}

build() {
    cd "$srcdir/${pkgname%-git}"
    make
}

package() {
    cd "$srcdir/${pkgname%-git}"
    install -Dm755  praat "$pkgdir/usr/bin/praat"
}
