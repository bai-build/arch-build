# Maintainer: Laurent Zhu <laurent.zhu@epita.fr>
# Contributor: Kevin Houdebert <kevin@qwazerty.eu>

_pkgname=monoburg
pkgname=${_pkgname}-git
pkgver=1.0.7
pkgrel=1
pkgdesc='MonoBURG is a tree-rewriting system originating from Mono.'
url='https://www.lrde.epita.fr/wiki/MonoBURG'
arch=('x86_64' 'i686')
license=('GPL3')
makedepends=('git')
depends=('glib2')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("git+https://gitlab.lrde.epita.fr/tiger/$_pkgname.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --always | sed 's|monoburg-||;s|-|.|g'
}

build() {
  cd "$srcdir/$_pkgname"
  ./bootstrap
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_pkgname"
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

check() {
  cd "$srcdir/$_pkgname"
  make check
}

# vim:set sts=2 sw=2 et:
