# Maintainer: kikadf <kikadf.01@gmail.com>

pkgname=clipgrab-kde
pkgver=3.8.14
_patchver=3.8.14
pkgrel=1
pkgdesc='A video downloader and converter for YouTube, Veoh, DailyMotion, MyVideo, ...'
arch=(x86_64)
url='https://clipgrab.org'
license=(GPL3)
depends=(qt5-webengine)
optdepends=('ffmpeg: for the conversion functionality')
makedepends=('qt5-tools')
conflicts=('clipgrab')
provides=('clipgrab')
source=(https://download.clipgrab.org/clipgrab-${pkgver}.tar.gz
        clipgrab.desktop
        https://gitlab.com/kikadf/clipgrab-qt5/raw/master/clipgrab-${_patchver}-close2tray.patch)
sha256sums=('cebf6b940133536abf8b0fa1f5913a614eadd0221e6003ad6d7752de253e63a4'
            '840910097d8c129ea12c395c4a33f562591e878e521c98fde8a7b3f9403aa23f'
            '54a3323588aa53bf90a42e219a9f9e686d6d61e24d29f89b0af6de3c1ab1e050')

prepare() {
  cd clipgrab-${pkgver}
  patch -p1 -i ../clipgrab-${_patchver}-close2tray.patch
  lrelease clipgrab.pro
  lupdate clipgrab.pro
}

build() {
  cd clipgrab-${pkgver}
  qmake \
    QMAKE_CFLAGS_RELEASE="$CPPFLAGS $CFLAGS" \
    QMAKE_CXXFLAGS_RELEASE="$CPPFLAGS $CXXFLAGS" \
    QMAKE_LFLAGS_RELEASE="$LDFLAGS" \
    clipgrab.pro
  make
}

package() {
  cd clipgrab-${pkgver}
  install -Dm755 clipgrab -t "$pkgdir"/usr/bin/
  install -Dm644 icon.png "$pkgdir/usr/share/pixmaps/clipgrab.png"
  install -Dm644 "$srcdir"/clipgrab.desktop -t "$pkgdir"/usr/share/applications/
}
