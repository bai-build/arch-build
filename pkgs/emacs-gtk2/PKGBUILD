# Maintainer: Alad Wenter <alad@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Renchi Raju <renchi@green.tam.uiuc.edu>
pkgname=emacs-gtk2
_pkgname=emacs
pkgver=26.3
pkgrel=1
pkgdesc="The extensible, customizable, self-documenting real-time display editor"
arch=('x86_64')
url="http://www.gnu.org/software/emacs/emacs.html"
license=('GPL3')
depends=('librsvg' 'gpm' 'giflib' 'libxpm' 'libotf' 'm17n-lib' 'gtk2'
         'hicolor-icon-theme' 'desktop-file-utils' 'alsa-lib' 'libmagick6' 'gnutls')
conflicts=('emacs')
provides=('emacs')
validpgpkeys=('B29426DEFB07724C3C35E5D36592E9A3A0B0F199' '28D3BED851FDF3AB57FEF93C233587A47C207910')
source=("ftp://ftp.gnu.org/gnu/emacs/$_pkgname-$pkgver.tar.xz"
        "ftp://ftp.gnu.org/gnu/emacs/$_pkgname-$pkgver.tar.xz.sig")
sha256sums=('4d90e6751ad8967822c6e092db07466b9d383ef1653feb2f95c93e7de66d3485'
            'SKIP')


build() {
    cd "$_pkgname-$pkgver"
    PKG_CONFIG_PATH="/usr/lib/imagemagick6/pkgconfig" \
    ac_cv_lib_gif_EGifPutExtensionLast=yes ./configure \
        --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib \
        --localstatedir=/var --with-x-toolkit=gtk2 --with-xft \
        --with-modules --without-gconf
    make
}

package() {
    cd "$_pkgname-$pkgver"
    make DESTDIR="$pkgdir" install

    # remove conflict with ctags package
    mv "$pkgdir"/usr/bin/{ctags,ctags.emacs}
    mv "$pkgdir"/usr/share/man/man1/{ctags.1.gz,ctags.emacs.1}

    # fix user/root permissions on usr/share files
    find "$pkgdir"/usr/share/emacs/$pkgver -exec chown root:root {} \;
}
