# Maintainer:  Vincent Grande <shoober420@gmail.com>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: lorim <lorimz@gmail.com>

pkgname=lib32-libtiff4-git
pkgver=3.9.7
pkgrel=1
pkgdesc='Library for manipulation of TIFF images'
arch=(x86_64)
url=http://www.simplesystems.org/libtiff/
license=(custom)
depends=(
  lib32-gcc-libs
  lib32-glibc
  lib32-libjpeg-turbo
  lib32-zlib
)
makedepends=(git)
provides=(lib32-libtiff4)
conflicts=(lib32-libtiff4)
source=(
  git+https://gitlab.com/libtiff/libtiff.git#branch=branch-3.9
  libtiff4-soname.patch
)
sha256sums=(
  SKIP
  SKIP
)

pkgver() {
  cd libtiff
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd libtiff

  # Rename 3.9.7 to 4.3.7, 3.6.x was the first release with the unintentional ABI change
  patch -Np1 -i ../libtiff4-soname.patch

  ./autogen.sh
}

build() {
  cd libtiff

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32'
  make
}

package() {
  make DESTDIR="${pkgdir}" -C libtiff install
  rm -rf "${pkgdir}"/usr/{bin,include,lib32/libtiff{,xx}.{a,so},share}
  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s libtiff4 "${pkgdir}"/usr/share/licenses/lib32-libtiff4
}

# vim: ts=2 sw=2 et:
