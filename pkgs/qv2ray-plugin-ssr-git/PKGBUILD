# Maintainer: DuckSoft <realducksoft at gmail dot com>
pkgname=qv2ray-plugin-ssr-git
_pkgname=qv2ray-plugin-ssr
pkgver=20200923.r84.adae383
pkgrel=1
pkgdesc="Qv2ray Plugin: ShadowsocksR"
arch=('x86_64')
url='https://github.com/Qv2ray/QvPlugin-SSR'
license=('GPL3')
# _virtualdepends=('qv2ray-plugin-host=1')
depends=('qt5-base>5.11.0' 'openssl' 'libuv' 'libsodium' 'qv2ray')
makedepends=('git' 'make' 'qt5-tools' 'which' 'gcc' 'cmake' 'ninja' 'libffi')
provides=('qv2ray-plugin-ssr')
groups=('qv2ray-plugin')
conflicts=('qv2ray-plugin-ssr')
source=("$_pkgname::git+${url}")
sha512sums=('SKIP')
pkgver() {
    cd "$srcdir"/"$_pkgname"
    local date=$(git log -1 --format="%cd" --date=short | sed s/-//g)
    local count=$(git rev-list --count HEAD)
    local commit=$(git rev-parse --short HEAD)
    echo "$date.r${count}.$commit"
}
prepare() {
    cd "$srcdir"/"$_pkgname"
    # no recursive here. arch don't need link statically libuv and libsodium.
    git submodule update --init
}
build() {
    cd "$srcdir"/"$_pkgname"
    mkdir -p build && cd build
    cmake .. \
        -DCMAKE_INSTALL_PREFIX="${pkgdir}"/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DSSR_UVW_WITH_QT=1 \
        -DUSE_SYSTEM_SODIUM=ON \
        -DUSE_SYSTEM_LIBUV=ON \
        -DSTATIC_LINK_LIBUV=OFF \
        -DSTATIC_LINK_SODIUM=OFF \
        -GNinja
    cmake --build .
}
package() {
    cd "$srcdir"/"$_pkgname"/build
    cmake --install .
    # NOTE: This virtual dependency will be introduced after Qv2ray stablize its interface.
    #     depends+=(${_virtualdepends[@]})
} 
