# Maintainer: Tucker Boniface <tucker@boniface.tech>
# Submitter: Alex Chamberlain <alex@alexchamberlain.co.uk>
# Maintainer: Kars Wang <jaklsy@gmail.com>

pkgname=jq-git
_gitname='jq'
pkgver=1.6rc2.3.g0c845aa
pkgrel=1
pkgdesc='Command-line JSON processor'
arch=('i686' 'x86_64')
url='http://stedolan.github.io/jq/'
license=('custom')
depends=('oniguruma')
makedepends=('python2' 'git' 'autoconf' 'automake' 'bison' 'flex' 'glibc')
provides=('jq')
conflicts=('jq')
source=('git+https://github.com/stedolan/jq.git')
sha512sums=('SKIP')

pkgver() {
    cd "$_gitname"
    git describe --tag | sed -e 's/^jq-//' -e 's/-/./g'
}

prepare() {
    cd "$_gitname"
}

build() {
    cd "$_gitname"

    autoreconf -i
    ./configure --prefix=/usr
    make
}

package() {
    cd "$_gitname"
    make DESTDIR="$pkgdir" prefix=/usr install
    install -Dm644 COPYING "${pkgdir}/usr/share/licenses/jq"
}
