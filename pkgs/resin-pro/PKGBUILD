# Maintainer: Zach Callear <zach@callear.org>
pkgname=resin-pro
pkgver=4.0.66
pkgrel=1
pkgdesc='Resin, a web server and Java EE application server from Caucho Technology'
arch=('x86_64')
url='https://caucho.com/products/resin'
license=('custom:Caucho Developer Source License')
depends=('java-runtime-headless')
makedepends=('java-environment')
source=("http://caucho.com/download/$pkgname-$pkgver.tar.gz")
sha256sums=(
    # Caucho does not provide hashes, so this is generated by the package maintainer:
    '854cd44b3bbd813aae2fe7a1981ea41c86b92b2d6f595689921b692de482582d'
)

build() {
	cd "$pkgname-$pkgver"
    # The path of the init.d script is from a comment in the resinctl script.
    # It's unused but left in place. This package doesn't yet provide a systemd
    # service.
    ./configure \
        --prefix=/usr/share/resin \
        --with-resin-root=/var/resin \
        --with-resin-log=/var/log/resin \
        --with-resin-conf=/etc/resin \
        --with-resin-init-d=/usr/share/resin/contrib/init.resin \
        --enable-64bit
	make
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir/" install
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
