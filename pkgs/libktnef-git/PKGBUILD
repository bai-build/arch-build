# Maintainer: Antonio Rojas <arojas@archlinux.org>

_gitname=libktnef
pkgname=$_gitname-git
pkgver=r271.572e41d
pkgrel=1
pkgdesc="API for handling TNEF data"
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/kde/pim/ktnef"
license=('LGPL')
depends=('kcalutils-git' 'kcontacts-git')
makedepends=('extra-cmake-modules-git' 'git' 'python')
conflicts=("$_gitname")
provides=("$_gitname")
source=("git://anongit.kde.org/ktnef.git")
md5sums=('SKIP')

pkgver() {
  cd ktnef
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../ktnef \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
