# Maintainer:  HÃ¥vard Pettersson <mail@haavard.me>

_pkgname=nss-tls
pkgname=$_pkgname-git
pkgver=r80.e31dd2a
pkgrel=1
pkgdesc='A DNS over HTTPS resolver for glibc'
url='https://github.com/dimkr/nss-tls'
arch=(i686 x86_64)
license=('LGPL2.1')
depends=(glib2 libsoup json-glib)
makedepends=(git meson)
provides=($_pkgname)
conflicts=($_pkgname)
source=("$_pkgname::git+https://github.com/dimkr/nss-tls.git")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build(){
  arch-meson $_pkgname build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}

# vim:set ts=2 sw=2 et:
