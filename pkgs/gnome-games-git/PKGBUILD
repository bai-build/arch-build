# Maintainer: Philip Goto <philip.goto@gmail.com>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Tom Richards <tom@tomrichards.net>
# Contributor: TingPing <tingping@tingping.se>

pkgname=gnome-games-git
pkgver=3.37.1.r20.g1ae9138f
pkgrel=1
pkgdesc='Simple game launcher for GNOME'
arch=(i686 x86_64 armv7h aarch64)
license=('GPL3')
url='https://wiki.gnome.org/Apps/Games'
depends=(grilo libhandy libmanette retro-gtk tracker)
makedepends=(git meson vala)
conflicts=(gnome-games)
provides=(gnome-games)
source=("git+https://gitlab.gnome.org/GNOME/gnome-games.git")
sha256sums=(SKIP)

pkgver() {
  cd gnome-games
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  arch-meson gnome-games build -D docs=true
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
