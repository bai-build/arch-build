# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Dumitru Ursu <dima@ceata.org>

pkgname=opencolorio-qfix
pkgver=2.0.1
pkgrel=3
pkgdesc="A color management framework for visual effects and animation"
arch=('x86_64')
url="https://opencolorio.org"
license=('BSD')
depends=('freeglut' 'glew' 'lcms2' 'tinyxml' 'yaml-cpp' 'imath' 'pystring')
makedepends=('boost' 'cmake' 'python' 'pybind11' 'ninja')
optdepends=('python: python bindings')
source=($pkgname-$pkgver.tar.gz::https://github.com/AcademySoftwareFoundation/OpenColorIO/archive/v$pkgver.tar.gz
        opencolorio-openexr3.patch)
sha512sums=('5787cc4cadb0a1832c925712ca0dfef6e843082b9461460e1798326c6fceaa97bee78e5298651e6a8a3662b348eab5ab9791e94c3efcac337da07229fb559338'
            '269a86e4020d272b64af631ba01f9b1e644bde119e9bfb1d5ec3fd62579d5d3f7ab31295fb6cffc51d7349c469e2ca6e12f69310317266eda602541e9fcff94f')

prepare() {
  patch -d OpenColorIO-$pkgver -p1 < opencolorio-openexr3.patch
}

build() {
  cd OpenColorIO-$pkgver

  cmake \
    -GNinja \
    -Bbuild \
    -DCMAKE_INSTALL_PREFIX=/opt/ocio \
    -DCMAKE_INSTALL_LIBDIR=lib
  ninja -C build
}

package() {
  cd OpenColorIO-$pkgver

  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
