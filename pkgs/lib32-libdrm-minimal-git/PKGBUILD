# Maintainer:  Vincent Grande <shoober420@gmail.com>
# Contributor: Lone_Wolf <lonewolf@klaas-de-kat.nl>
# Contributor: Eric Engestrom <eric@engestrom.ch>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=lib32-libdrm-minimal-git
pkgver=2.4.100.r69.gfe06ee20
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services, git 32-bit version"
arch=(x86_64)
license=('custom')
depends=('libdrm-git' 'lib32-libpciaccess' 'lib32-gcc-libs')
makedepends=('meson' 'git' 'ninja')
checkdepends=('lib32-cairo')
url="http://dri.freedesktop.org/"
provides=('lib32-libdrm')
conflicts=('lib32-libdrm')

source=("git+https://gitlab.freedesktop.org/mesa/drm"
        COPYING)
sha256sums=('SKIP'
            '9631d4f694952e3e6ae5a05534c2e93e994e47d3413677a3a00e45c8cef6db93')

          
pkgver() {
    cd drm
    git describe --long --abbrev=8 | sed 's/^libdrm-//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    if [  -d _build ]; then
        rm -rf _build
    fi
}

build() {
    export CC="gcc -m32"
    export CXX="g++ -m32"
    export PKG_CONFIG=/usr/bin/i686-pc-linux-gnu-pkg-config
    meson setup drm _build \
        --prefix /usr \
        --libdir lib32 \
        --buildtype plain \
        --wrap-mode      nofallback \
        -D udev=false \
        -D valgrind=false \
	-D freedreno=false \
	-D vc4=false \
	-D vmwgfx=false \
	-D amdgpu=true \
	-D radeon=true \
	-D intel=false \
	-D nouveau=false \
	-D man-pages=false
    meson configure _build
    ninja $NINJAFLAGS -C _build

# EDIT BUILD OPTIONS TO MATCH YOUR CARDS - DEFAULTS TO AMDGPU / RADEON
# RADEON NEEDS TO BE TRUE WHEN BUILDING AGAINST MESA
}

#check() {
   # '-t 10' is needed for the 'threaded' test, which uses the default meson
   # test timeout of 30 seconds. This is too short for many systems. It can be
   # removed if upstream fixes the issue.
#    meson test -C _build -t 10
#}

package() {
    DESTDIR="$pkgdir" ninja $NINJAFLAGS -C _build install
  
    # remove files already provided by libdrm-git
    rm -rf "$pkgdir"/usr/{include,share,bin}
  
    install -Dt "$pkgdir/usr/share/licenses/$pkgname"  -m644 COPYING
}
