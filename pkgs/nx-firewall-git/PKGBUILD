# Maintainer: kang <kang at insecure dot ws>
# Contributor: Original PKGBUILD from nomad-firewall-git maz-1 <ohmygod19993 at gmail dot com>

_pkgname=nx-firewall
pkgname=nx-firewall-git
pkgver=0.1.r110.g17551b7
pkgrel=2
pkgdesc="kcm module for firewall from nx desktop"
arch=('i686' 'x86_64')
url="https://github.com/nx-desktop/nx-firewall"
license=('GPL3')
depends=('kdeclarative' 'kcoreaddons' 'kcmutils' 'ki18n' 'plasma-framework' 'kdelibs4support'
        'kauth' 'kconfig' 'kio' 'knewstuff' 'kconfigwidgets' 'net-tools' 'ufw' 'kdesignerplugin' 'kinit')
makedepends=('git' 'extra-cmake-modules' 'kdoctools')
replaces=('nomad-firewall-git')
source=('git+https://github.com/nx-desktop/nx-firewall.git')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  mkdir -p "$srcdir/build"
  cd "$srcdir/build"

# Release uses -Werror and the git code isn't all that clean, use that when they are actual releases
#    -DCMAKE_BUILD_TYPE=Release \
  cmake "$srcdir/$_pkgname" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd "$srcdir/build"
  make DESTDIR="$pkgdir/" install
  ln -s kcms/org.nxos.firewall.so "$pkgdir/usr/lib/qt/plugins/org.nxos.firewall.so"
}
