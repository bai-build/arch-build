# Maintainer: Tobias Bachmann <tobachmann@gmx.de>
pkgname=connectome-workbench
pkgver=1.5.0
pkgrel=3
pkgdesc="Connectome Workbench is an open source, freely available visualization and discovery tool used to map neuroimaging data, especially data generated by the Human Connectome Project."
arch=('x86_64')
url="http://humanconnectome.org/software/connectome-workbench"
license=('GPL')
depends=('qt5-base')
makedepends=('cmake')
source=("https://github.com/Washington-University/workbench/archive/v$pkgver.tar.gz"
        "$pkgname.desktop"
        "001-add_missing_limits.patch::https://github.com/Washington-University/workbench/commit/2605e8a98d23d229d54a9e6847ad64f678217ea7.patch")
sha256sums=('6410f5cdaed2158d99871962fb27ce9fdfad6fa7dad496b93ff8bce0a8070cc4'
            'dc8385561d8b65a9cb0179254bc5c0d66a808dcfb2a5a2c11dd1d1716c7b8c23'
            'f3e53ea453217ade168fcf37978321fc09980362520e81774fcdeb73afb03f64')

prepare() {
  cd "workbench-$pkgver"
  patch -Np1 -i $srcdir/001-add_missing_limits.patch
}

build() {
  mkdir build
  cd build
  cmake -D CMAKE_INSTALL_PREFIX=/usr -D CMAKE_BUILD_TYPE=Release -D WORKBENCH_MESA_DIR=/usr -D WORKBENCH_USE_QT5=TRUE ../workbench-$pkgver/src
  make -j $(nproc)
}

check() {
  cd build
  ctest
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
  mkdir -p $pkgdir/usr/share/$pkgname/icons
  install ../workbench-$pkgver/icons/linux/workbench_128x128x32.png $pkgdir/usr/share/$pkgname/icons/workbench_128x128x32.png
  mkdir -p $pkgdir/usr/share/applications
  install ../../$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
}

post_install() {
  update-desktop-database
}
