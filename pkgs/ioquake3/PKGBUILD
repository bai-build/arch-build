# Contributor: Slash <demodevil5[at]yahoo[dot]com>

pkgname=ioquake3
pkgver=1.36
pkgrel=2
pkgdesc="The de-facto OSS Quake 3 distribution. You need the retail/demo .pk3 files to play."
url="http://ioquake3.org/"
license=('GPL')
arch=('i686' 'x86_64')
depends=('sdl' 'openal')
makedepends=('nasm')
conflicts=('quake3' 'quake3-icculus-svn' 'quake3-svn' 'ioquake3-svn')
provides=('quake3')
replaces=('quake3')
install=quake3.install
source=('quake3.desktop' 'quake3.launcher' 'quake3ded.launcher' \
"http://www.ioquake3.org/files/${pkgver}/${pkgname}-${pkgver}.tar.bz2" \
'http://ftp.gwdg.de/pub/misc/ftp.idsoftware.com/idstuff/quake3/linux/linuxq3apoint-1.32b-3.x86.run' \
'botlib.patch::http://bugzilla-attachments.icculus.org/attachment.cgi?id=2196'
'warnings.patch')
sha256sums=('12dbd31e9de1493642d120bfd1548dfc4935e47fec806003cfc04b9d84b85673'
            '7f0806379b10a6cff248b647aeb67d89a8e954e51e7a633cfadeca9865d125e7'
            '7f0806379b10a6cff248b647aeb67d89a8e954e51e7a633cfadeca9865d125e7'
            '79b10b92e9e586748a67089ac871646fe8144004d3ab4a55158348bc38d74821'
            'c36132c5556b35e01950f1e9c646235033a5130f87ad776ba2bc7becf4f4f186'
            'd4bef896dc9fc7e8b8c94dcb744f88c03f76f5025deacce50de2cf7945fa71d3'
            'ecdac6d433d4a9400eb3c5549de10ec76e23b9d4a26e580524f5e3d6d1cc5181')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # Patch Makefile to correct install path
    /bin/sed -i "s:/usr/local/games/quake3:${pkgdir}/opt/quake3:" Makefile

    # Patch botlib so bots work on 64bit
    patch -p1 < "${srcdir}/botlib.patch"

    # Patch to fix GCC warnings
    patch -p1 < "${srcdir}/warnings.patch"
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # Compile ioQuake3
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # Install Files
    make copyfiles

    # Extract Patch Files
    cd "${srcdir}"
    chmod +x "${srcdir}/linuxq3apoint-1.32b-3.x86.run"
    "${srcdir}/linuxq3apoint-1.32b-3.x86.run" --tar xf

    # Modify Launcher Scripts
    if [ "$CARCH" = "x86_64" ]; then
        #
        # x86_64 Systems
        #
        /bin/sed -i "s:IOQ3_BINARY:ioquake3.x86_64:" \
            "${srcdir}/quake3.launcher"
        /bin/sed -i "s:IOQ3_BINARY:ioq3ded.x86_64:" \
            "${srcdir}/quake3ded.launcher"
    else
        #
        # i686 Systems
        #
        /bin/sed -i "s:IOQ3_BINARY:ioquake3.i386:" \
            "${srcdir}/quake3.launcher"
        /bin/sed -i "s:IOQ3_BINARY:ioq3ded.i386:" \
            "${srcdir}/quake3ded.launcher"
    fi

    # Install Quake 3 Patch Files
    install -m 644 "${srcdir}"/baseq3/*.pk3 \
        "${pkgdir}/opt/quake3/baseq3/"

    # Install Quake 3 Expansion Pack Patch Files
    install -m 644 "${srcdir}"/missionpack/*.pk3 \
        "${pkgdir}/opt/quake3/missionpack/"

    # Install Launcher (Client)
    install -D -m 755 "${srcdir}/quake3.launcher" \
        "${pkgdir}/usr/bin/quake3"

    # Install Launcher (Server)
    install -D -m 755 "${srcdir}/quake3ded.launcher" \
        "${pkgdir}/usr/bin/quake3ded"

    # Install Desktop File
    install -D -m 644 "${srcdir}/quake3.desktop" \
        "${pkgdir}/usr/share/applications/quake3.desktop"

    # Install Icon File
    install -D -m 644 "${srcdir}/$pkgname-$pkgver/misc/quake3.png" \
        "${pkgdir}/usr/share/pixmaps/quake3.png"
}

