# Maintainer: Bruce Zhang
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgname=qtspim-iconfix
_pkgname=qtspim
pkgver=9.1.21
pkgrel=1
pkgdesc="New user interface for spim, a MIPS simulator."
arch=('x86_64')
url="http://spimsimulator.sourceforge.net/"
license=('BSD')
depends=('qt5-base')
makedepends=('qt5-tools' 'subversion' 'icu')
source=("spimsimulator::svn://svn.code.sf.net/p/spimsimulator/code/#revision=729")
sha256sums=('SKIP')
provides=('qtspim')
conflicts=('qtspim')

prepare() {
  cd spimsimulator/QtSpim

  sed -r \
    -e 's/-Wno-write-strings/& -fpermissive/' \
    -i QtSpim.pro
  rm parser_yacc.* scanner_lex.*

  cd ../Setup/qtspim_debian_deployment
  sed -i "s|Icon=/usr/lib/qtspim/qtspim.png|Icon=qtspim|g" qtspim.desktop
}

build() {
  cd spimsimulator/QtSpim

  qmake

  # Fix Makefile generated by qmake.
  sed -i 's#$(MOVE) help/qtspim.qhc help/qtspim.qhc;##' Makefile

  make -j1
}

package() {
  cd spimsimulator

  install -D QtSpim/QtSpim "$pkgdir/usr/bin/qtspim"

  install -d "$pkgdir/usr/share/qtspim"
  cp -r QtSpim/help "$pkgdir/usr/share/qtspim"

  install -Dm644 Documentation/spim.man "$pkgdir/usr/share/man/man1/qtspim.1"
  install -Dm644 Setup/qtspim_debian_deployment/qtspim.desktop \
    "$pkgdir/usr/share/applications/qtspim.desktop"
  install -Dm644 Setup/qtspim_debian_deployment/copyright \
    "$pkgdir/usr/share/licenses/$pkgname/copyright"
  install -Dm644 Setup/NewIcon48x48.png "$pkgdir/usr/share/icons/hicolor/48x48/apps/qtspim.png"
  install -Dm644 Setup/NewIcon256x256.png "$pkgdir/usr/share/icons/hicolor/256x256/apps/qtspim.png"
}
