# Maintainer: Bruno Pagani (a.k.a. ArchangeGabriel) <bruno.n.pagani at gmail dot com>

pkgbase=calligra
pkgname=krita
pkgver=2.9.9
pkgrel=2
pkgdesc="Digital painting and illustration suite (stand-alone)."
arch=('i686' 'x86_64')
url="http://www.krita.org/"
license=('GPL2')
depends=('eigen' 'fftw' 'glew' 'gsl' 'kdebase-runtime' 'libkdcraw4' 'opencolorio' 'boost-libs' 'curl' 'poppler-qt4')
makedepends=('automoc4' 'boost' 'cmake' 'lcms2' 'openjpeg' 'vc')
provides=('calligra-krita')
conflicts=('calligra-krita' 'calligra-plugins' 'calligra-libs' 'calligra-extras' 'calligra-filters')
install=krita.install
source=("http://download.kde.org/stable/${pkgbase}-${pkgver}/${pkgbase}-${pkgver}.tar.xz")
sha256sums=('6e9d1251883c9cc5ad17afbe38959dce5ae1adbec7221831733d0f15fc83e521')

prepare() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  mkdir -p ${pkgname}-build
}

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}/${pkgname}-build"
  cmake ../ \
    -Wno-dev \
    -DCMAKE_BUILD_TYPE=Release \
    -DPRODUCTSET=KRITA \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DWITH_Soprano=OFF
  make
}

package_krita() {
  cd "${srcdir}/${pkgbase}-${pkgver}/${pkgname}-build"
  make DESTDIR="$pkgdir" install
}
