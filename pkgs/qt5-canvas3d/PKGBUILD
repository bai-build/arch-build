# Maintainer : bartus <arch-user-repoá˜“bartus.33mail.com>
# Contributor : Felix Yan <felixonmars@archlinux.org>
# Contributor : Andrea Scarpino <andrea@archlinux.org>
# shellcheck disable=SC2034

pkgname=qt5-canvas3d
_qtver=5.12.4
pkgver=${_qtver/-/}
pkgrel=1
arch=('x86_64')
url='https://www.qt.io'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='A JavaScript 3D rendering API for Qt Quick (deprecated in qt 5.13)'
depends=('qt5-declarative')
makedepends=()
groups=('qt' 'qt5')
_pkgfqn="${pkgname/5-/}-everywhere-src-${_qtver}"
source=("https://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz")
sha256sums=('d7e0e8aa542d077a929fb7700411ca9de1f65ae4748d64168d2e7533facd7869')

prepare() {
  mkdir -p build
}

build() {
  cd build

  qmake ../${_pkgfqn}
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
