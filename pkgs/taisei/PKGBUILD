# Maintainer: Lukas Weber <laochailan web de>
# Contributor: Andrei Alexeyev <akari taisei-project org>
# Contributor: cyberpunkrocker <l33tmmx gmail com>
# Contributor: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG

pkgname=taisei
pkgver=1.3.2
pkgrel=1
pkgdesc="Open source Touhou clone"
arch=('i686' 'x86_64')
url="https://taisei-project.org/"
license=('MIT')
depends=('opengl-driver' 'cglm' 'sdl2_mixer' 'freetype2' 'libpng' 'libwebp' 'libzip' 'hicolor-icon-theme')
makedepends=('meson' 'python-docutils')
source=("https://github.com/taisei-project/taisei/releases/download/v$pkgver/taisei-v$pkgver.tar.xz")
sha256sums=('dbc05f1b5c31981d8711130ac283355b7bfad403895f4096a6bb7e9a3d73a3bc')

build() {
    cd $pkgname-v$pkgver
    arch-meson build
    ninja -C build 
}

package() {
    cd $pkgname-v$pkgver
    DESTDIR="$pkgdir" ninja install -C build
    install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
