# Maintainer: VerruckteFuchs <derverrucktefuchs@protonmail.ch>
# Original compton-git PKGBUILD and aur package maintained by WorMzy Tykashi <wormzy.tykashi@gmail.com>
pkgname=compton-blackcapcoder-git
_gitname=compton
pkgver=0.1_beta2.120.gdd5837b
pkgrel=1
pkgdesc="BlackCapCoder's patched fork of compton"
arch=(i686 x86_64)
url="https://github.com/BlackCapCoder/compton"
license=('MIT')
depends=('libgl' 'libdbus' 'libxcomposite' 'libxdamage' 'libxrandr' 'pcre' 'libconfig' 'libxinerama' 'hicolor-icon-theme')
makedepends=('git' 'asciidoc' 'mesa')
optdepends=('dbus:          To control compton via D-Bus'
            'xorg-xwininfo: For compton-trans'
            'xorg-xprop:    For compton-trans')
provides=('compton')
conflicts=('compton')
source=("git://github.com/BlackCapCoder/compton.git")
md5sums=("SKIP")

pkgver() {
    cd "${srcdir}/${_gitname}"
    git describe --tags | sed -e 's:v::' -e 's/-/./g'
}

build() {
  cd "$srcdir/$_gitname"
  make PREFIX=/usr
  make docs
}

package() {
  cd "$srcdir/$_gitname"

  make PREFIX="$pkgdir/usr" install

  # install license
  install -D -m644 "LICENSE" "$pkgdir/usr/share/licenses/$_gitname/LICENSE"

  # example conf
  install -D -m644 "compton.sample.conf" "$pkgdir/etc/xdg/compton.conf.example"
}
