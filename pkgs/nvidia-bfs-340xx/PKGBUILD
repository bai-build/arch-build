# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>

pkgname=nvidia-bfs-340xx
pkgver=340.93
_extramodules=extramodules-4.3-bfs
pkgrel=6
_pkgdesc="NVIDIA 340xx drivers for linux-bfs."
pkgdesc="$_pkgdesc"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-bfs>=4.3' 'linux-bfs<4.4' "nvidia-340xx-libgl" "nvidia-340xx-utils=${pkgver}")
makedepends=('linux-bfs-headers>=4.3' 'linux-bfs-headers<4.4')
conflicts=('nvidia-bfs' 'nvidia-bfs-304xx')
license=('custom')
install=nvidia-bfs-340xx.install
options=(!strip)
source=("nv-drm.patch"
        'nvidia-4.3-build.patch')
source_i686=("ftp://download.nvidia.com/XFree86/Linux-x86/${pkgver}/NVIDIA-Linux-x86-${pkgver}.run")
source_x86_64=("ftp://download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/NVIDIA-Linux-x86_64-${pkgver}-no-compat32.run")
md5sums=('2365f1405f0c7bbb8f8cd7ebd5e4e301'
         'e400a8c538afd490726941d8c69b2c2d')
md5sums_i686=('4a7edf6838a80c77a57fb6f6ec7f0437')
md5sums_x86_64=('cb80e3f1cb6f2fb6e6eab35fad0884e4')

[[ "$CARCH" = "i686" ]] && _pkg="NVIDIA-Linux-x86-${pkgver}"
[[ "$CARCH" = "x86_64" ]] && _pkg="NVIDIA-Linux-x86_64-${pkgver}-no-compat32"

prepare() {
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}"
    patch -Np0 -i "$srcdir/nv-drm.patch"
    patch -Np1 -i "$srcdir/nvidia-4.3-build.patch"
}

build() {
	_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
	cd "${_pkg}/kernel"
	make SYSSRC=/usr/lib/modules/"${_kernver}/build" module

	cd uvm
	make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
	install -Dm644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
		"${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
	install -D -m644 "${srcdir}/${_pkg}/kernel/uvm/nvidia-uvm.ko" \
		"${pkgdir}/usr/lib/modules/${_extramodules}/nvidia-uvm.ko"
	gzip -9 "${pkgdir}/usr/lib/modules/${_extramodules}/"*.ko
	install -dm755 "${pkgdir}/usr/lib/modprobe.d"
	echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia-bfs-340xx.conf"
}
