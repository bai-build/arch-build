# Maintainer: Evert Vorster <evorster@gmail.com>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: IncredibleLaser
# Contributor: nous <nous at archlinux.us>
# Contributor: pressh

pkgname=kdenlive-applications15.08-git
pkgver=15.08.0.r6255.4a8b014
pkgrel=1
pkgdesc="A non-linear video editor for Linux using the MLT video framework. KF5 Frameworks (GIT Version, Applications/15.08 branch)"
arch=('i686' 'x86_64')
url="http://www.kdenlive.org/"
license=('GPL')
depends=('glu' 'knewstuff' 'kplotting' 'knotifyconfig' 'mlt' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'kdoctools' 'git' 'v4l-utils')
optdepends=('ffmpeg: for FFmpeg plugin'
            'cdrkit: for creation of DVD ISO images'
            'dvdauthor: for creation of DVD'
            'dvgrab: for firewire capture'
            'libdv: for webcam capture (if FFmpeg is not installed)'
            'recordmydesktop: for screen capture'
            'xine-ui: for DVD preview')
provides=('kdenlive')
conflicts=('kdenlive')
source=('git://anongit.kde.org/kdenlive#branch=Applications/15.08') 
sha1sums=('SKIP')
install=$pkgname.install

pkgver() {
  cd kdenlive
  _ver="$(cat CMakeLists.txt | grep KDE_APPLICATIONS_VERSION | head -3 | cut -d "\"" -f2 | tr "\n" "." | cut -d "." -f1-3)"
  echo "$(echo ${_ver}).r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare(){
  mkdir -p build
}

build() {
  cd build
  cmake ../kdenlive \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}

