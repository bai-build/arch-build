# Maintainer: Lone_Wolf <lonewolf@xs4all.nl>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=libglvnd12
_pkgname=libglvnd
pkgver=1.2.0
pkgrel=2
pkgdesc="The GL Vendor-Neutral Dispatch library"
arch=('x86_64')
url="https://github.com/NVIDIA/libglvnd"
license=('custom:BSD-like')
makedepends=('libxext' 'libx11' 'glproto' 'python')
provides=('libgl' 'libegl' 'libgles' 'libglvnd')
conflicts=('libglvnd')
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/NVIDIA/libglvnd/archive/v${pkgver}.tar.gz"
        LICENSE)
sha512sums=('0e0e9c3f303739c73f5bee4c2aaef9eff56440345f2f1ce285ee98eb94c1e5e740774dd501aeeed1299aa25a449d813a850e95282999a3ca4648d2381d6c8fa6'
            'bf0f4a7e04220a407400f89226ecc1f798cc43035f2538cc8860e5088e1f84140baf0d4b0b28f66e4b802d4d6925769a1297c24e1ba39c1c093902b2931781a5')

build() {
  cd "${_pkgname}-${pkgver}"
  ./autogen.sh
  ./configure --prefix=/usr \
    --disable-headers
  make
}

package() {
  # libglvnd needs mesa for indirect rendering
  depends=('libxext' 'mesa-git' 'opengl-driver')

  cd "${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  install -Dm644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
