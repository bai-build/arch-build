# Maintainer: Kazuo Teramoto <kaz.rag at gmail dot com>
# Contributor: RÃ©my Oudompheng <remy@archlinux.org>
# Contributor: Firmicus <francois.archlinux.org>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: bender02 at gmx dot com

pkgname=asymptote-svn
pkgver=r5724
pkgrel=1
pkgdesc="A vector graphics language (like metapost)"
arch=('i686' 'x86_64')
url="http://asymptote.sourceforge.net/"
license=('GPL3')
depends=('texlive-core' 'gc' 'freeglut' 'glu' 'gsl' 'fftw' 'mesa')
makedepends=('svn' 'ghostscript' 'imagemagick' 'texlive-plainextra')
optdepends=('python2:           for the xasy GUI'
            'python-imaging:    for the xasy GUI'
            'tix:               for the xasy GUI')
conflicts=('asymptote')
provides=('asymptote=2.33svn')
install=texlive.install
source=('asymptote::svn+http://svn.code.sf.net/p/asymptote/code/trunk')
md5sums=('SKIP')
_svnmod=asymptote

pkgver() {
  cd "$srcdir/${_svnmod}"
  local ver="$(svnversion)"
  printf "r%s" "${ver//[[:alpha:]]}"
}

build() {
  cd "$srcdir/$_svnmod/${_svnmod}"

  ./autogen.sh
  ./configure --enable-gc=/usr \
    --prefix=/usr \
    --with-latex=/usr/share/texmf/tex/latex \
    --with-context=/usr/share/texmf/tex/context \
    --enable-offscreen

  make all
}

check() {
  cd "$srcdir/$_svnmod/${_svnmod}"
  make check-all
}

package() {
  cd "$srcdir/$_svnmod/${_svnmod}"
  make -j1 DESTDIR="${pkgdir}" install-all
  sed -i -e 's@env python@env python2@' ${pkgdir}/usr/share/asymptote/GUI/*.py
  # this dir contains png files that are already embedded in the pdf documentation:
  rm -rf ${pkgdir}/usr/share/info/asymptote

  # move vim files to correct place
  install -dm755 ${pkgdir}/usr/share/vim/vimfiles/{ftdetect,syntax}
  mv ${pkgdir}/usr/share/asymptote/asy.vim \
    ${pkgdir}/usr/share/vim/vimfiles/syntax/asy.vim
  mv ${pkgdir}/usr/share/asymptote/asy_filetype.vim \
    ${pkgdir}/usr/share/vim/vimfiles/ftdetect/asy.vim
}
