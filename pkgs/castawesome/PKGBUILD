# Maintainer: Joey Dumont <joey.dumont@gmail.com>
# Contributor Dominik Pieczy≈Ñski <dominik.pieczynski@gmail.com>
# Contributor: Alex Mason <alex@judgementstudios.com>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

pkgname=castawesome
pkgver=0.16.0
pkgrel=1
arch=('any')
pkgdesc='A GUI frontend for ffmpeg livestreaming'
url='https://github.com/TheSamsai/Castawesome'
license=('GPL3')
depends=('ffmpeg' 'gtk3' 'python' 'python-gobject')
source=("https://github.com/TheSamsai/Castawesome/releases/download/$pkgver/$pkgname-$pkgver.tar.gz"
        "Makefile.patch")
sha256sums=('d2cc2a705d8143dafabdc43a4e169b2c15e7856512da9c823c700ec60a03cd57'
            'cddaf5eba8a8569d3bb82a5ce2c9c99517c23e3d3550ac03b6b03533f48c9301')

prepare() {
  cd $pkgname

  sed -i 's#cp castawesome.py#install -Dm 755 castawesome.py#' Makefile
  sed -i 's#/usr/local#$(DESTDIR)/usr#g' Makefile
  sed -i 's#/usr/local#/usr#' castawesome.py
  sed -i 's#/usr/local#/usr#' uninstall_castawesome.sh

  patch -p1 -i $srcdir/Makefile.patch
}

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/share/castawesome"
  make DESTDIR="$pkgdir" install

  rm -rf "$pkgdir/usr/share/applications"
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
