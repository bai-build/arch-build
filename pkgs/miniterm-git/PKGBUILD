# Maintainer: Justin Frank <justinpfrank@protonmail.com>

pkgname=miniterm-git
_pkgname=miniterm
pkgver=v1.7.0.0.gbdd12c3
pkgrel=1
pkgdesc="Lightweight VTE terminal emulator with colorscheme support (fork of tinyterm)"
arch=('i386' 'x86_64')
url="https://github.com/laelath/miniterm"
license=('MIT')
depends=('vte3' 'glib2')
makedepends=('git' 'cmake')
source=('git+https://github.com/laelath/miniterm.git')
md5sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  git describe --tags --long | sed 's/-/./g'
}

build() {
  cd "$_pkgname"
  mkdir -p Release
  cd Release
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr ..
  make
}

package() {
  cd "$_pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  cd Release
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
