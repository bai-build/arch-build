# Maintainer: Shengyu Zhang <la@archlinuxcn.org>

pkgname=srain-dev
pkgver=1.0.0rc2.1112.1c367df
_pkgver=1.0.0rc2
pkgrel=1
pkgdesc="Modern IRC client, develop and debug version"
arch=('i686' 'x86_64')
license=('GPL')
url="https://srain.im"
makedepends=('git' 'make' 'gcc' 'pkg-config' 'gettext' 'python-sphinx')
depends=('glib2' 'gtk3' 'libconfig' 'libsoup' 'gdb')
optdepends=(
    'glib-networking: TLS connection support'
    )
conflicts=('srain')
provides=('srain')
source=("git+https://github.com/SrainApp/srain.git#branch=dev/1.0")
sha256sums=('SKIP')
options=('!strip')

_prefix='/usr'

pkgver() {
    cd ${pkgname%-dev}
    echo "${_pkgver}.$(git rev-list --count HEAD).$(git describe --always)"
}

build() {
    cd ${pkgname%-dev}

    ./configure                         \
        --prefix="${_prefix}"           \
        --datadir="${_prefix}/share"    \
        --sysconfdir="/etc"             \
        --enable-debug
    make
    make doc
}

package() {
    cd ${pkgname%-dev}

    make DESTDIR="${pkgdir}" install
    make DESTDIR="${pkgdir}" install-doc
    mv "${pkgdir}${_prefix}/bin/srain"{,-dev}

    cat << EOF > "${pkgdir}${_prefix}/bin/srain"
#!/bin/sh
gdb "${_prefix}/bin/srain-dev" -ex "r \$@" -ex "bt" -ex "q"
EOF

    chmod 755 "${pkgdir}${_prefix}/bin/srain"
}
