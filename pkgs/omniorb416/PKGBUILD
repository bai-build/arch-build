# Maintainer: Michele Mocciola <mickele>
# Contributor: Sven Schneider <archlinux.sandmann@googlemail.com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Essien Ita Essien <me@essienitaessien.com>
# Contributor: Brice Mealier <mealier_brice@yahoo.fr>

pkgname=omniorb416
pkgver=4.1.6
pkgrel=3
pkgdesc="A CORBA object request broker for C++ and Python. Legacy version."
arch=('i686' 'x86_64')
url="http://omniorb.sourceforge.net/"
license=('GPL2' 'LGPL2')
provides=('omniorb=4.1.6')
conflicts=('omniorb')
depends=('python2' 'openssl')
makedepends=('pkgconfig')
source=("http://downloads.sourceforge.net/omniorb/omniORB-${pkgver}.tar.bz2" "openssl-1.1.diff")

prepare() {
  cd "${srcdir}/omniORB-${pkgver}"

  patch -Np1 -i "${srcdir}/openssl-1.1.diff"

  # python2 fix
  for file in $(find . -name '*.py' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' "$file"
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' "$file"
  done
}

build() {
  cd "${srcdir}/omniORB-${pkgver}"

  PYTHON=/usr/bin/python2.7 ./configure \
         --prefix=/usr \
         --with-omniORB-config=/etc/omniorb/omniORB.cfg \
         --with-omniNames-logdir=/var/log/omniORB \
         --with-openssl=/usr

  make
}

package() {
  cd "${srcdir}/omniORB-${pkgver}"

  make DESTDIR="${pkgdir}" install
  
  install -D -m 644 sample.cfg "${pkgdir}/etc/omniORB.cfg"

  for i in man/man1/*.1; do
    install -D -m 644 $i "${pkgdir}/usr/share/${i}"
  done
  chmod 755 "${pkgdir}"/{usr,usr/bin,usr/lib,usr/share,usr/include,usr/share/idl,usr/lib/pkgconfig,usr/lib/python2.7,usr/lib/python2.7/site-packages}
}
md5sums=('44990f8139c349b53ab43110de6c629b'
         'cfd55fea5a6442e1c9b2230db616d0b8')
