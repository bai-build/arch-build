# Maintainer: Dominik Heidler <dominik@heidler.eu>

pkgname=lua-ldbus
pkgver=r118.a6e77a0
pkgrel=1
pkgdesc="Lua Bindings to dbus."
arch=('x86_64' 'i686')
url="https://github.com/daurnimator/ldbus"
license=('MIT')
groups=()
provides=()
depends=('lua' 'libdbus')
makedepends=('luarocks' 'git')

_commit="a6e77a080131b6e7f6fb0db27141fac5658f6b07"

source=("git://github.com/daurnimator/ldbus.git#commit=${_commit}")
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/ldbus"
	( set -o pipefail
		git describe --long --tags 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
		printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	)
}

build() {
	cd "$srcdir/ldbus"
	luarocks make --pack-binary-rock ldbus-scm-0.rockspec \
		DBUS_INCDIR=/usr/include/dbus-1.0/ \
		DBUS_ARCH_INCDIR=/usr/lib/dbus-1.0/include
}

package() {
	cd "$srcdir/ldbus"
	install -Dvsm755 ldbus.so ${pkgdir}/usr/lib/lua/5.3/ldbus.so
}
