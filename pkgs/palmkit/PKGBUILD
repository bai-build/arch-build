# Maintainer: Tokuhiro Matsuno <tokuhirom@gmail.com>
pkgname=palmkit
pkgver=1.0.32
pkgrel=1
pkgdesc="Publicly Available Language Modeling Toolkit"
arch=('x86_64')
url="http://palmkit.sourceforge.net/"
license=("unknown")
depends=("awk" "ruby")
makedepends=("autoconf")
source=("http://palmkit.sourceforge.net/palmkit-$pkgver.tar.gz")
sha256sums=("bbd26daa4c04104fdbc7fa7e45d8aec551cdf362c2c5ca882529d85bb61fa962")

prepare() {
    cd "$pkgname-$pkgver/src"
    sed -i 's/LDFLAGS=@LIBS@/LDFLAGS=@LDFLAGS@ @LIBS@/' Makefile.in
}

build() {
    cd "$pkgname-$pkgver/src"
    # palmkit contains configure file, generated by very old autoconf.
    # it's not compatible with latest gcc.
    autoconf
    ./configure --enable-zlib --enable-largefile
    make
}

package() {
    cd "$pkgname-$pkgver/src"
    make install
    cd ..
    mkdir -p $pkgdir/usr/bin
    mkdir -p $pkgdir/usr/include
    mkdir -p $pkgdir/usr/lib
    install bin/* $pkgdir/usr/bin/
    install include/* $pkgdir/usr/include/
    install --mode=644 lib/* $pkgdir/usr/lib/
}

